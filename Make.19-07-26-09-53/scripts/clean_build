#!/bin/csh -f

# set echo 

set num_args = $#argv

if (${num_args} < 1) then
    ${P_STDERR} "Script clean_build needs a lead argument for the directory."
    ${P_STDERR} "'NULL', '.', and './' are all OK."
    exit 1 
endif 

if (("$1" != "") && ("$1" != " ") && ("$1" != ".") && ("$1" != "./")) then
    set parent_dir = "$1"
    # Ensure exactly one trailing slash.
    set parent_dir = "${parent_dir}/" 
    set parent_dir = `echo ${parent_dir} | sed 's#//*#/#g'`
    cd ${parent_dir}
    echo cleaning in ${parent_dir}
else 
    echo cleaning in ${cwd}
    set parent_dir = ""
endif 

if ($?KJB_VERBOSE) then
    set noglob
    echo Script clean_build will loop over: foreach arg: ${argv[2-$#argv]} 
    unset noglob 
endif 

foreach item (${argv[2-$#argv]}) 
    if ($?KJB_VERBOSE) then
        set noglob
        echo Script clean_build processing item: $item
        unset noglob
    endif 

    set item_is_link = `${MAKE_SCRIPT_PATH}is_link ${item}`
                
    if (-d "${item}") then
        # Ensure exactly one trailing slash.
        set item = "${item}/" 
        set item = `echo ${item} | sed 's#//*#/#g'`

        # echo cleaning ${parent_dir}${item}
        set file_count = `/bin/ls -1 "${item}" | grep -c '.' `
        # echo cleaning ${file_count} files.

        if ($?KJB_VERBOSE) then
            echo Script clean_build cleaning ${file_count} files in directory item: $item
        endif 

        if (${file_count} == 0) then
            set start_with_slash = `echo ${item} | grep -c '^/'`
            
            if (${start_with_slash}) then
                echo ${item} is already clean. 
            else
                echo ${parent_dir}${item} is already clean. 
            endif 
        else 
            set noglob 
            echo Script clean_build executing ${KJB_RM} ${item}*
            unset noglob
            ${KJB_RM}  ${item}*  
        endif 
    else if (! "${item_is_link}") then
        set strip_name = `echo ${item} | sed 's#.*/##'`

        # echo "strip_name: $strip_name (in `pwd`)"

        if (("${strip_name}" != "${item}") && ("${strip_name}" != "") && (-e "${strip_name}")) then
            set strip_name_is_link = `${MAKE_SCRIPT_PATH}is_link ${strip_name}`

            if (${strip_name_is_link}) then 
                echo Script clean_build removing link ${strip_name}
                ${KJB_RM}  ${strip_name}
            else 
                echo "Script clean_build not removing ${strip_name} as planned as it exists and is not a link."
            endif 
        endif 

        if (-e "${item}") then
            echo "Script clean_build executing ${KJB_RM}  ${item}"
            ${KJB_RM} ${item} 
        else
            echo ${item} is already clean. 
        endif 
    else 
        if ($?KJB_VERBOSE) then
            echo "Script clean_build not doing anything for: $item (bug?)"
        endif 
    endif 
end 

