#!/bin/csh -f

# set echo 

if ($?KJB_LIB_PATH == 0) then
    if ($?KJB_SRC_PATH == 0) then
        ${P_STDERR} "Script do_ctags assumes that either KJB_LIB_PATH or KJB_SRC_PATH is set."
        exit 1
    else 
        pushd ${KJB_SRC_PATH} > /dev/null
            setenv KJB_LIB_PATH ${cwd}/lib/
        popd > /dev/null 
    endif 
endif 

@ num_args = ${#argv}

if ("${num_args}" > 1) then
    ${P_STDERR} "Script do_ctags takes only 0 or 1 parameters."
    exit 1 
else if ("${num_args}" == 1) then
    if ("${1}" != "") then
        pushd "${1}" > /dev/null
    endif 
endif 



(${KJB_LS} -1 *.[ch] |& grep -v "No match" > tag_files) >& /dev/null


if (-e Include_lines/sub_libs_needed) then
    set sub_libs_needed = `/bin/cat Include_lines/sub_libs_needed`

    foreach sub_lib_needed ($sub_libs_needed)
        if (-d ${KJB_LIB_PATH}/${sub_lib_needed}) then
             ${KJB_LS} -1 ${KJB_LIB_PATH}/${sub_lib_needed}/*.[ch]  |& grep -v "No match"  >> tag_files
        else if (-d lib/${sub_lib_needed}) then
             ${KJB_LS} -1 lib/${sub_lib_needed}/*.[ch]  |& grep -v "No match"  >> tag_files
        else if (-d ../../lib/${sub_lib_needed}) then
             ${KJB_LS} -1 ../../lib/${sub_lib_needed}/*.[ch]  |& grep -v "No match"  >> tag_files
        endif
    end
endif 

set exit_status = 0

${KJB_CAT} /dev/null > tags
if (${status}) then 
    set exit_status = 1
else 
    #
    # In current exhuberant ctags, sort=yes is the default. GNU ctags chokes on
    # it. Hence we no longer use it. However, GNU ctags also chokes on -L, which
    # we do not have a convenient work around for.
    #
    # ctags --sort=yes -a -L tag_files -o tags
    # ctags -a -L tag_files -o tags
    # Testing new ctags c++ support (probably these new flags won't work with GNU ctags, either)
    ctags -a -L tag_files --c++-kinds=+p --fields=+iaS --extra=+q -o tags
    if (${status}) then
        echo "ctags failed in script do_ctags."
        echo "Possibly ctags on this machine is an old flavor that we don't care about."
        set exit_status = 1
    endif 
endif 

${KJB_RM} tag_files >& /dev/null

exit ${exit_status}


