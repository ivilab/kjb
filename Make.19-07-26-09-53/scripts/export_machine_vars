#!/bin/csh -f 

###############################################################################
#
# Identification:
#     A script to get some compile vars into bash scripts. 
#
# Description:
#     A script to get some compile vars into bash scripts. In the bash scripts
#     one uses:
# |        eval `path_to_this_file`
#     e.g.
# |        eval `/home/kobus/src/Make/scripts/export_machine_vars`
#     This sets:
# |      MACHINE, UC_MACHINE, UC_OS
#
#     Currently we only export the above variables set by init_machine for
#     Kobus's courses so that students can try the kjb library without using the
#     source.
#
#     However, if we need things like KJB_CC and OBJ_DIR, we could extend this
#     script, or write a similar script, that consults init_compile as well. 
#
# Author:
#     Kobus Barnard
#
###############################################################################


# set echo
set non_local_places = "/home/kobus/src /net/v04/work/kobus/src /net/v07/misc/src"

set found = 0
set src_dir = ""

pushd `pwd` > /dev/null

while ("`pwd`" != "/")
    if (-e Make/init_machine) then
        set src_dir = "${cwd}"
        break
    endif 

    cd ..
end

popd > /dev/null

if ("${src_dir}" == "") then

    foreach non_local_place (${non_local_places}) 
        if (-e ${non_local_place}/Make/init_machine) then
            set src_dir = "${non_local_place}"
            break
        endif 
    end
endif 


if ("${src_dir}" == "") then
   # We cannot use P_STDERR here because we might not have defined it yet.
   bash -c "echo 1>&2"
   bash -c "echo This directory does not seem to be below a src dirctory with KJB installed. 1>&2"

   foreach non_local_place (${non_local_places}) 
       bash -c "echo Nor does ${non_local_place}/Make/init_machine exist. 1>&2"
   end

   bash -c "echo  1>&2"

   exit 1 
endif 

# unset echo 
# echo src_dir:${src_dir}

source ${src_dir}/Make/init_machine

echo "export MACHINE=${MACHINE}; export UC_MACHINE=${UC_MACHINE}; export UC_OS=${UC_OS}"


